#!/usr/bin/env perl
package App::Unding;

use strict;
use warnings;
# Autor: Boris DÃ¤ppen, 2018
# No guarantee given, use at own risk and will

# PODNAME: app-unding
# ABSTRACT: dark magic encrypted wallet

use feature 'say';

use File::Slurp;
use Data::Serializer;
#cpanm Crypt::CBC
#cpanm Crypt::Blowfish

my  $DATA_ptr;
our $old_state; # what was in DATA, when script started
our $new_state; # what should be in DATA, after script ends
our $encrypt_mode = 0;

INIT {
    $DATA_ptr = tell DATA;        # where is DATA?
    $old_state = join "", <DATA>; # slurp
}

my $help_message = <<'END_HELP';

  Encrypt a file. Content will be stored in unding.
  Attention: File will remain on disk!

      unding /path/to/file

  Decrypt and display content stored in unding.

      unding

END_HELP

if (      # decrypt (no arg given) but nothing stored
          (not defined $ARGV[0] and (length($old_state) < 1))
          # encrypt (arg given) but --help instead of path
       or (defined $ARGV[0] and $ARGV[0] =~ /^(-h|--help)/)
   ) {
     print STDERR $help_message;
     exit 1;
}

print 'Password: ';
my $password = <STDIN>;

my $obj = Data::Serializer->new(
                   serializer => 'Data::Dumper',
                   digester   => 'SHA-256',
                   cipher     => 'Blowfish',
                   secret     => $password,
                   portable   => '1',
                   compress   => '0',
             serializer_token => '1',
                     options  => {},
                  );

if ( defined $ARGV[0] ) {
    $encrypt_mode = 1;
    my $filename = $ARGV[0];
    my $data = read_file($filename);
    $new_state =$obj->serialize($data);
}
else {
    my $data =$obj->deserialize($old_state);
    if (defined $data) {
        say $data;
    }
    else {
        say STDERR 'Wrong password?';
    }
}


END {
    if ($encrypt_mode) {
        open DATA, '+<', $0;   # $0 is the name of this script
        seek DATA, $DATA_ptr, 0;
        print DATA $new_state;
        truncate DATA, tell DATA;  # in case new data is shorter than old data
        close DATA;
    }
}

=pod

=encoding utf8

=head1 SYNOPSIS

Encrypt a file. Content will be stored in unding.
Attention: File will remain on disk!

 unding /path/to/file

Decrypt and display content stored in unding.

 unding

=cut

__DATA__
